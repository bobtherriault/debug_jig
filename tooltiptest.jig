cocurrent'tjig' NB. Set the defining locale as tjig

CSS=: 0 : 0  NB. CSS for the different types and animations of tootips.
<style type="text/css">
 svg {overflow:visible;}
 rect {overflow:visible;fill:#DDD;}
 text { font-family:"courier"; font-weight:bold; stroke-width:0.75; stroke:black; pointer-events: none; text-anchor:start; white-space:pre;}
 .i  { font-weight:normal; stroke:none;}
 rect:hover  {fill:#96C; opacity:1; transition: all 0.1s ease-in-out;}
</style>
)


DISPLAY=: 0 : 0 NB. Form that contains the display as well as zoom and font buttons
pc tooltest;
minwh 250 170; cc w2 webview;set _ sizepolicy minimum;
)

webdisplay=: 3 : 0 NB. Displays the results in webview for jqt environment and returns ID of window
 1 webdisplay y
:
 wd DISPLAY 
 wd 'set w2 wh *', ": s=. 250 170>. (_225 _200 + 2 3 { ". wd 'qscreen') <. SCALE * > {: x
 wd 'setp wh ',": s + 100 65
 wd 'set w2 html *', y
 wd 'pshow'
)

tooltest_close=:  wd bind 'pclose;'

visual=: 3 : 0 
tm=. (0;0;0;a:) insvg i. 3 4
'fW fH'=.   (sc 200 120) + 3 5 {::"0 _ tm 
tm=.  ; (3&{. , ":@(3&{::) ; 4&{ , ":@(5&{::) ; {:) tm  NB. changes format on width and height 
tm=.'<svg width="',(": SCALE * fW),'" height="',(": SCALE * fH),'" viewbox="',(cnv sc _180) ,' ',(cnv sc _85),' ', (": fW,fH),'" preserveAspectRatio="xMidYMin meet" xmlns="http://www.w3.org/2000/svg" xmlns:xlink="http://www.w3.org/1999/xlink" >',tm,'</svg>'
( 100 + fW , fH) webdisplay htmpack tm   
)

SCALE=: 1
FONT=: 'courier'
sc=: 3 : '((1 >. % SCALE)* ])  y'
htmpack=: 3 :'''<hmtl><head><meta charset="UTF-8">'', (CSS rplc ''<FONT>'';FONT),''</head><body>'', y ,''</body></html>'''
tooltip=: (' onclick="alert(''' ,"1 ,"1 & ''')" ><title>' ,"1  ,"1 & '</title></rect>')  NB. fork that takes tt as argument to insert into onclick and tooltip string
lbtt=: 3 : ';"1 {&a. each (<194 160) (I. @:((<32)=])) }"1 boxutf y' NB. substitutes 194 160 for 32 in tooltips for nbsp
cnv=:,/ @: > @: (8!:0)"1 NB. converts _20 to -20 for svg text and justifies appropriately
mask=:(-@<:@# }. ((,@#,:) , 0:)/) @ (}:@$ , 1:)


insvg=: 4 : 0 NB. numeric case - integer 
'depth s b path'=. x 
tt=.'<h1> Integer <br> Shape: ',((''-:  $ y){::((":  $ y);'atom')),(; b{'';(' <br> <br> Depth: ',(":depth),' <br> Path: ',(}: path))),' </h1>'
tm=. ;(<'<text class="i" x="4" y="'),. (cnv each 20+ 18x *(# i.@#)@mask y),. (<'" textLength="'),. (<cnv tl=.<. 96r10 * {:$":y),. (<'" lengthAdjust="spacingAndGlyphs" >'),. (,. , <"1  ": y) ,. <'</text>'
'<rect class="';((1-: {. $ y){'jd');'" width="';(<.9 + tl);'" height="';(14 + 18x * # mask  y);'" rx="6"',(tooltip tt),tm
)

NB. visual ''  NB. remove NB. to display the example
